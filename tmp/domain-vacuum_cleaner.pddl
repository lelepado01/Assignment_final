;; domain file: domain-vacuum_cleaner.pddl
(define (domain vacuum_cleaner)
    (:requirements :strips)
    (:predicates
        (in ?from ?robot)
        (is_room ?from)
        (is_robot ?robot)
        (is_battery_level ?battery_level)
        (battery_lower_level ?battery_level ?battery_lower)
        (has_battery_level ?robot ?battery_level)
        (rooms_connected ?from ?to)
        (dirty ?room)
        (clean ?room)
        (has_charge_pad ?room)              
    )
    
        (:action Move
            :parameters (?from ?to ?robot ?battery_level ?battery_lower)
            :precondition (and
                (in ?from ?robot)
                (is_room ?from)
                (is_room ?to)
                (is_robot ?robot)
                (is_battery_level ?battery_level)
                (is_battery_level ?battery_lower)
                (battery_lower_level ?battery_level ?battery_lower)
                (has_battery_level ?robot ?battery_level)
                (rooms_connected ?from ?to)
            )
            :effect (and
                (in ?to ?robot)
                (not (in ?from ?robot))
                (not (has_battery_level ?robot ?battery_level))
                (has_battery_level ?robot ?battery_lower)
            )
        )
        
        (:action Clean
            :parameters (?room ?robot ?battery_level ?battery_lower)
            :precondition (and
                (in ?room ?robot)
                (is_room ?room)
                (is_robot ?robot)
                (dirty ?room)
                (is_battery_level ?battery_level)
                (is_battery_level ?battery_lower)
                (battery_lower_level ?battery_level ?battery_lower)
                (has_battery_level ?robot ?battery_level)
            )
            :effect (and
                (clean ?room)
                (not (dirty ?room))
                (not (has_battery_level ?robot ?battery_level))
                (has_battery_level ?robot ?battery_lower)
            )
        )
        
        (:action Charge
            :parameters (?room ?robot ?battery_level ?battery_lower)
            :precondition (and
                (in ?room ?robot)
                (is_room ?room)
                (is_robot ?robot)
                (has_charge_pad ?room)
                (is_battery_level ?battery_level)
                (is_battery_level ?battery_lower)
                (battery_lower_level ?battery_level ?battery_lower)
                (has_battery_level ?robot ?battery_lower)
            )
            :effect (and
                (not (has_battery_level ?robot ?battery_lower))
                (has_battery_level ?robot ?battery_level)
            )
        )
)